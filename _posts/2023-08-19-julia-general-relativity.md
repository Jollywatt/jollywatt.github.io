---
layout: post
title: "Basic general relativity with Julia"
author: "Joseph Wilson"
categories: math
tags: []
---

<style>
figure img {
  max-height: 350px;
}
</style>

Four years after I took general relativity, my professor asked me to send him an old animation I had made during his course showing _parallel transport of vectors around a line of latitude on the sphere_.
Actually, I wasnâ€™t the one who had made it (though I was flattered that he had thought so!)

The tool I reached for was Julia. To my delight, the whole thing was rather simple to make.
This was the humble result:

<video src="{{ site.github.url }}/projects/julia-general-relativity/sphere-transport.mp4" width="70%" autoplay muted loop></video>

While taking that course, I remember thinking that numerically computing geodesics or simulating parallel transport were frightfully involved and required a mess of technical computing. Not so.
If you have similar impression, read on.

This post describes how you might roll your own code for doing basic numerical general relativity (or rather, differential geometry) while only depending on external packages for dual numbers, differential equation solvers, and plotting.

## Contents
{:.no_toc}

* hello
{:toc}

## Step 0: Maps between manifolds

<!-- When deciding how to deal with manifolds and maps between them with a computer, there were two main directions to go: represent them symbolically and use a CAS to do all the algebra; or represent them numerically.

I chose to stick with numbers. This allows us to 
 -->
The very first task is to decide how to represent the relevant mathematical objects: manifolds, homomorphisms between them and tensor fields.
These things can seem complicated, but given a choice of coordinates, a point in a manifold is just a `Vector` of coordinates, a homomorphism is a function taking a `Vector` argument and returning a `Vector`, and tensor fields are the same thing, but returning a multidimensional `Array`. 

For example, hereâ€™s an embedding $$f: ð•Š^2 â†’ â„^3$$ of a sphere into 3D space using the usual spherical coordinates $$(Î¸, Ï†)$$, as a Julia function:

```julia
f((Î¸, Ï†)) = [
  sin(Î¸)cos(Ï†)
  sin(Î¸)sin(Ï†)
  cos(Î¸)
]
```

Notice this has one vector argument which is de-structured into two scalars.

```julia
julia> f([Ï€/2, 0]) # meridian point on the equator
3-element Vector{Float64}:
 1.0
 0.0
 6.123233995736766e-17
```

Hereâ€™s how you might use this embedding to plot a surface with [`Makie.jl`](https://juliapackages.com/p/Makie):

```julia
using GLMakie
points = [f([Î¸, Ï†]) for Î¸ âˆˆ range(0, Ï€, 32), Ï† âˆˆ range(0, 2Ï€, 64)]
x, y, z = eachslice(stack(points), dims=1)
wireframe(x, y, z, transparency=true, alpha=0.1, color=:blue)
surface!(x, y, z, transparency=true, alpha=0.2)
```

<figure>
  <img src="{{ site.github.url }}/projects/julia-general-relativity/sphere.png">
</figure>

You could imagine doing something more involved like defining an object-oriented zoo of types for manifolds, coordinate charts, transition functions, homomorphisms, tensor fields, and so on. Donâ€™t bother. Dead simple is good.

## Step 1: Automatic differentiation

Differential geometry has lots of â€œ$$âˆ‚$$â€s.
We want to have derivatives of homomorphisms such as `f` be computed _automatically_.
There are many approaches to doing this with Julia, ranging from symbolic differentiation with [`Symbolics.jl`](https://juliapackages.com/p/Symbolics) to using [`Zygote.jl`](https://juliapackages.com/p/Zygote) to differentiate code.

In our case, weâ€™re not doing anything fancy â€” just building vectors with mathematical functions line `sin` â€” so all weâ€™ll need is an implementation of dual numbers.

### Dual numbers

If you extend the reals by adding an element $$Îµ â‰  0$$ satisfying $$Îµ^2 = 0$$, then you can get derivative information for free when you evaluate a function with $$+Îµ$$, since

$$
f(x + Îµ) = f(x) + Îµ f'(x)
$$

by Taylorâ€™s theorem.
The function value is the real part, and its derivative is the coefficient of $$Îµ$$. If we denote this coefficient by $$âŸ¨\quadâŸ©_Îµ$$, we have our dual number derivative formula:

$$
f'(x) = âŸ¨f(x + Îµ)âŸ©_Îµ
$$

To do this in Julia, the [`TaylorSeries.jl`](https://juliapackages.com/p/TaylorSeries) package is sufficient. 
Hereâ€™s computing the derivative of $$\sin(x)$$ at $$x = \frac{Ï€}{3}$$:

```julia
julia> using TaylorSeries

julia> sin(Taylor1([Ï€/3, 1]))
 0.8660254037844386 + 0.5000000000000001 t + ð’ª(tÂ²)

julia> ans.coeffs[2] # get the Îµ term
0.5000000000000001

julia> ans == cos(Ï€/3)
true
```

To make accessing the $$Îµ$$ coefficient clearer, we may define

```julia
epsilonpart(a::Taylor1{T}) where T = get(a.coeffs, 2, zero(T))
```

which works even if, for some reason, `a` is truncated to $$ð’ª(1)$$ so that `a.coeffs` has no second component.

We can pack this into a function to take the derivative of functions on $$â„$$.

```julia
âˆ‚(f, x::Number) = epsilonpart.(f(Taylor1([x, 1])))
```

which would be used like so:

```julia
julia> âˆ‚(sin, 1) â‰ˆ cos(1)
true
```

### Computing Jacobians

This trick works for functions of vectors, too.
We just add $$+Îµ$$ to the component we want to differentiate with respect to:

$$
âˆ‚_Î¼ f(\vec x) = \biggâŸ¨ f\left(\begin{bmatrix}x^1 \\ \vdots \\ x^Î¼ + Îµ \\ \vdots \\ x^n \end{bmatrix}\right) \biggâŸ©_Îµ
$$

For a vector-valued function $$f : â„^m â†’ â„^n$$, we have a matrix of derivatives (a Jacobian).

$$
âˆ‚f(\vec x) â‰”
\mqty[
âˆ‚_Î¼ f^Î½(\vec x)
]_{Î¼Î½} =
\mqty[
âˆ‚_1 f(\vec x) \\
\vdots \\
âˆ‚_m f(\vec x)
] =
\mqty[
âˆ‚_1 f^1(\vec x) & \cdots & âˆ‚_1 f^n(\vec x) \\
\vdots & \ddots & \vdots \\
âˆ‚_m f^1(\vec x) & \cdots & âˆ‚_n f^n(\vec x)
]
$$

_Note._ The Jacobian is conventionally the transpose of this, but we would like to have the first dimension (along columns) to run along the first index that appears, $$Î¼$$, and the second dimension (along rows) along the the second index, $$Î½$$.
This means indices in code will appear in the same order as in the maths, making the generalisation to higher order tensors easier.
{: .aside}

Spelling this out with our dual number derivative formula:

$$
âˆ‚f(\vec x) =
\mqty[
\biggâŸ¨f^1\qty(\mqty[x^1 + Îµ\\\vdots\\x^n])\biggâŸ©_Îµ & \cdots & \biggâŸ¨f^m\qty(\mqty[x^1 + Îµ\\\vdots\\x^n])\biggâŸ©_Îµ \\
\vdots & \ddots & \vdots \\
\biggâŸ¨f^1\qty(\mqty[x^1\\\vdots\\x^n + Îµ])\biggâŸ©_Îµ & \cdots & \biggâŸ¨f^m\qty(\mqty[x^1\\\vdots\\x^n + Îµ])\biggâŸ©_Îµ \\
]
$$

Hereâ€™s a literal Julia implementation:

```julia
function âˆ‚(f, x::AbstractArray)
  I = eachindex(x)
  derivs = [epsilonpart.(f([Taylor1([x[i], i == j]) for i âˆˆ I])) for j âˆˆ I]
  stack(derivs, dims=1) # stack vectors as rows in a matrix
end
```

Letâ€™s test it on our sphere embedding map `f`.

```julia
julia> âˆ‚(f, [Ï€/2, 0])
2Ã—3 Matrix{Float64}:
 6.12323e-17  0.0  -1.0
 0.0          1.0  -0.0
```

The quantity $$âˆ‚_Î¼ f^Î½ (x)$$ is then given by `âˆ‚(f, x)[Î¼,âˆ¨]`.
We can check it is correct by comparing it to the analytic Jacobian:

```julia
julia> âˆ‚f((Î¸, Ï†)) = [
           +cos(Î¸)cos(Ï†) +cos(Î¸)sin(Ï†) -sin(Î¸)
           -sin(Î¸)sin(Ï†) +sin(Î¸)cos(Ï†)       0
       ];

julia> all(âˆ‚(f, x) â‰ˆ âˆ‚f(x) for x in eachcol(rand(2,100)))
true
```
So far so good.

### Higher order derivatives

But what about second order derivatives?

Luckily, since this is Julia, `TaylorSeries.jl` is generic and lets you construct Taylor series whose coefficients are themselves Taylor series.

This means our implementation for `âˆ‚` works even on functions which call `âˆ‚`, letting us compute higher order derivatives in the obvious way.
For example, $$âˆ‚_Î¼ âˆ‚_Î½ f^Î»(\vec x)$$ may be evaluated as:

```julia
âˆ‚âˆ‚f(x) = âˆ‚(xâ€² -> âˆ‚(f, xâ€²), x)
```

Notice that this produces a three dimensional array, since $$âˆ‚_Î¼ âˆ‚_Î½ f^Î»(\vec x)$$ has three indices.

```julia
julia> âˆ‚âˆ‚f([1,2])
2Ã—2Ã—3 Array{Float64, 3}:
[:, :, 1] =
  0.350175  -0.491295
 -0.491295   0.350175

[:, :, 2] =
 -0.765147  -0.224845
 -0.224845  -0.765147

[:, :, 3] =
 -0.540302  -0.0
 -0.0       -0.0
```

This shows how `âˆ‚` diverges slightly from other Jacobian implementations: ours doesnâ€™t flatten extra dimensions in order to produce a matrix.


## Step 2: Metrics and Christoffel symbols

To do things like compute geodesics and simulate parallel transport, we need the metric $$g_{Î¼Î½}$$ and Christoffel symbols $$Î“^Î»{}_{Î¼Î½}$$ for our manifold.


### Induced metrics for embedded manifolds

If our manifold happens to be a parametric surface embedded in $$â„^n$$, such as our sphere embedding $$f : ð•Š^2 â†’ â„^3$$, then we should be able to compute the metric on the surface induced by the ambient Euclidean metric, $$Î· = \operatorname{diag}(1, \dots, 1)$$.
Specifically, the induced metric $$g$$ is the _pullback_ of $$Î·$$ by $$f$$,

$$
g â‰” f^*Î·
.$$

The pullback $$f^*$$ is defined so that the above is equivalent to

$$
g(\vec u, \vec v) â‰” Î·(f_*\vec u, f_*\vec v)
$$

for any input vectors, where $$f_*$$ is the _pushforward_ â€” nothing but the Jacobian matrix from before.

$$
f_* \big|_{\vec x} â‰¡ âˆ‚f(\vec x)
$$

With coordinates, the action on a vector may be written as

$$
(f_* \vec u )^Î½\big|_{\vec x} = âˆ‚_Î¼ f^Î½(\vec x) \, u^Î¼
$$

so that the component form of $$g(\vec u, \vec v)$$ is:

$$
\begin{align*}
g_{Î¼Î½} u^Î¼ v^Î½ &= Î·_{ÏÏƒ} \, âˆ‚_Î¼ f^Ï u^Î¼ \, âˆ‚_Î½ f^Ïƒ u^Ï \\
g_{Î¼Î½} &= âˆ‚_Ï f^Î¼ \, Î·_{Î¼Î½} \, âˆ‚_Ïƒ f^Î½ \\
\end{align*}
$$

Really, this is just matrix-matrix multiplication.

$$
g = f_* Î· (f_*)^\intercal = f_* (f_*)^\intercal
$$

The Julia translation is just as pretty:

```julia
g(x) = let âˆ‚f = âˆ‚(f, x)
  âˆ‚f*âˆ‚f'
end
```

This should give us the metric on the sphere.
Letâ€™s sanity check it again:

```julia
julia> g_sphere((Î¸, Ï•)) = [
           1        0
           0 sin(Î¸)^2
       ];

julia> all(g(x) â‰ˆ g_sphere(x) for x in eachcol(rand(2, 100)))
true
```

### Christoffel symbols

Now comes the interesting maths.
The Christoffel symbols are defined in terms of the metric and its first derivatives as

$$
Î“^Î»{}_{Î¼Î½} = \frac12g^{Î»Ïƒ}\qty(\partial_Î¼ g_{ÏƒÎ½} + \partial_Î½ g_{ÏƒÎ¼} - \partial_Ïƒ g_{Î¼Î½})
.$$

Implementing this in Julia is just a matter of summing over all the right components.
We could do this with `sum(... for Î¼ âˆˆ 1:n)`, but a nice way of doing this is to use a package like [`TensorOperations.jl`](https://juliapackages.com/p/TensorOperations) which provides a macro enabling Einstein summation notation.

```julia
using TensorOperations

function Î“(g, x)
  âˆ‚g = âˆ‚(g, x) # 3-index array
  @tensor Î“[Î»,Î¼,Î½] := 2\inv(g(x))[Î»,Ïƒ]*(âˆ‚g[Î¼,Ïƒ,Î½] + âˆ‚g[Î½,Ïƒ,Î¼] - âˆ‚g[Ïƒ,Î¼,Î½])
end
```

Within the `@tensor` expression, any Julia identifier inside `[ ]` is treated as an index, whose range is automatically computed at runtime, with repeated indices summed.
The `:=` means a new array is created, whereas `=` would write into an existing array.

To test this out and check that the basic symmetry $$Î“^Î»{}_{Î¼Î½} = Î“^Î»{}_{Î½Î¼}$$ holds:

```julia
julia> Î“(g, [1,2])
2Ã—2Ã—2 Array{Float64, 3}:
[:, :, 1] =
 0.0  0.0
 0.0  0.642093

[:, :, 2] =
 0.0       -0.454649
 0.642093   0.0

julia> ans == permutedims(ans, (1, 3, 2))
true
```

## Step 3: Geodesics

Now we get to the cool stuff â€” geodesics.

A geodesic is a curve $$\vec x(t)$$ which satisfies the _geodesic equation_,

$$
\dot x^Î¼(t) âˆ‡_Î¼ \dot x^Î»(t) = 0
.$$

In words, the velocity vector $$\dot x$$ is covariantly constant along the direction it points.
Unpacking the covariant derivative, this becomes:

$$
\dot x^Î¼(t) âˆ‚_Î¼ \dot x^Î»(t) + \dot x^Î¼(t) Î“^Î»{}_{Î¼Î½} \big|_{\vec x(t)} \dot x^Î½(t) = 0
$$

Simplifying using the chain rule $$\frac{\dd x^Î¼}{\dd t}\frac{âˆ‚\dot{x}^Î»}{âˆ‚x^Î¼} = \ddot x^Î»$$ and leaving the position and $$t$$-dependence implicit, we have a system of second order ordinary differential equations

$$
\ddot x^Î» = -Î“^Î»{}_{Î¼Î½} \dot x^Î¼ \dot x^Î½
.$$

Julia has good support for numerically solving ODEs.
We will use the [`DifferentialEquations.jl`](https://juliapackages.com/p/DifferentialEquations) package.
It requires the ODE to be put into standard form,
$$
\ddot{x} = f_\text{geodesic}(\dot{x}, x, g, t)
.$$
The third argument $$g$$ can contain any extra data needed to evaluate $$\ddot{x}$$, which is just the metric in our case.

Additionally, the first argument of the Julia function is the array `xÌˆ` to write the derivatives into (optional, but leads to better performance).

```julia
function geodesic!(xÌˆ, áº‹, x, g, t)
  @tensor xÌˆ[Î»] = -Î“(g, x)[Î»,Î¼,Î½]*áº‹[Î¼]*áº‹[Î½]
end
```

<div class="aside" markdown=1>
  _Solving ODEs in Julia._

  Hereâ€™s an example of numerically solving the geodesic equation with some initial data.
  Take our sphere, and imagine launching a particle eastward along the equator, $$Î¸ = Ï€/2$$.

  ```julia
  julia> using DifferentialEquations

  julia> áº‹0 = [0., 1.]; x0 = [Ï€/2, 0.]; tspan = (0, 10);

  julia> prob = SecondOrderODEProblem(geodesic!, áº‹0, x0, tspan, g)
  ODEProblem with uType ArrayPartition{Float64, Tuple{Vector{Float64}, Vector{Float64}}} and tType Int64. In-place: true
  timespan: (0, 10)
  u0: ([0.0, 1.0], [1.5707963267948966, 0.0])

  julia> sol = solve(prob);
  ```

  The solution object `sol` is matrix-like, with four rows and $$n$$ columns

  $$
  \mqty[
  \dot{\vec x}(t_1) & \cdots & \dot{\vec x}(t_n) \\
  \vec x(t_1) & \cdots & \vec x(t_n) \\
  ]
  ,$$

  so we can extract just $$\vec x(t)$$ by taking the bottom two rows.
  ```julia
  julia> sol[3:4,:]
  2Ã—6 Matrix{Float64}:
   1.5708  1.5708      1.5708     1.5708    1.5708    1.5708
   0.0     0.00141306  0.0155436  0.156849  1.56991  10.0
  ```

  We can see that our geodesic curve stays at the equator $$Î¸ = Ï€/2$$ and orbits around with $$Ï†$$ increasing at one radian per unit time.

  Boring, but correct!

</div>


### Interesting example: Doughnut geodesics

Letâ€™s combine what we have so far to compute some geodesic curves on a torus.

The code is presented as a script for readability, but if you wanted to use it, you should pack everything into functions to avoid global variables and use something like [`Revise.jl`](https://juliapackages.com/p/Revise).

```julia
using TaylorSeries
using TensorOperations
using DifferentialEquations
using GLMakie

epsilonpart(a::Taylor1{T}) where T = get(a.coeffs, 2, zero(T))

function âˆ‚(f, x)
  I = eachindex(x)
  derivs = [epsilonpart.(f([Taylor1([x[i], i == j]) for i âˆˆ I])) for j âˆˆ I]
  stack(derivs, dims=1) # stack vectors as rows in a matrix
end

function Î“(g, x)
  âˆ‚G = âˆ‚(g, x)
  @tensor Î“[Î»,Î¼,Î½] := 2\inv(g(x))[Î»,Ïƒ]*(âˆ‚G[Î¼,Ïƒ,Î½] + âˆ‚G[Î½,Ïƒ,Î¼] - âˆ‚G[Ïƒ,Î¼,Î½])
end

function geodesic!(xÌˆ, áº‹, x, g, t)
  @tensor xÌˆ[Î»] = -Î“(g, x)[Î»,Î¼,Î½]*áº‹[Î¼]*áº‹[Î½]
end

R = 2
r = 1.5

# parametric torus
f((Î¸, Ï†)) = [
  (R + r*cos(Ï†))*cos(Î¸),
  (R + r*cos(Ï†))*sin(Î¸),
  r*sin(Ï†),
]

# embedded torus metric
g(x) = let df = âˆ‚(f, x)
  df*df'
end

# plot torus
circle = range(0, 2Ï€, length=64 + 1)
x, y, z = eachslice(stack([f([Î¸, Ï†]) for Î¸ âˆˆ circle, Ï† âˆˆ circle]), dims=1)
wireframe(x, y, z, transparency=true, alpha=0.1, color=:blue)
surface!(x, y, z, transparency=true, alpha=0.2)

# compute geodesic
prob = SecondOrderODEProblem(geodesic!, [0.06, 1], [0., 0.], (0, 20), g)
sol = solve(prob)

# plot geodesic
path = mapslices(f, sol(0:0.1:30)[3:4,:], dims=1)
lines!(path, linewidth=3, color=:black)
```

<figure>
  <img src="{{ site.github.url }}/projects/julia-general-relativity/torus-geodesic.png"/>
</figure>

## Step 4: Parallel transport

The other cool thing you can do once you have a metric is slide things along a path $$Î³(t)$$ irrotationally.

If $$\vec U(t)$$ is a vector defined everywhere on the path $$Î³(t)$$, then it is the parallel transport of some initial vector $$U(0)$$ if it satisfies the _parallel transport equation_,

$$
\dot Î³^Î¼(t) âˆ‡_Î¼ U^Î»(t) = 0
.$$

Like for the geodesic equation, unpacking the covariant derivative leads to the system of differential equations

$$
\dot Î³^Î¼(t) âˆ‚_Î¼ U^Î»(t) + \dot Î³^Î¼(t) Î“^Î»{}_{Î¼Î½}\big|_{Î³(t)} U^Î½(t) = 0
$$

which, more succinctly, is

$$
\dot U^Î» = -Î“^Î»{}_{Î¼Î½} \dot Î³^Î¼ U^Î½
.$$

This is only a first order differential equation, but now we have two pieces of data to provide as parameters: the metric $$g$$ and the path, $$Î³$$.

In Julia, the differential equation function looks like

```julia
function paralleltrans!(uÌ‡, u, (g, Î³), t)
  Î³Ì‡ = âˆ‚(Î³, t)
  @tensor uÌ‡[Î»] = -Î“(g, Î³(t))[Î»,Î¼,Î½]*Î³Ì‡[Î¼]*u[Î½]
end
```

and solving it, for the sphere example, looks like:

```julia
julia> f((Î¸, Ï†)) = [sin(Î¸)cos(Ï†), sin(Î¸)sin(Ï†), cos(Î¸)];

julia> g(x) = let âˆ‚f = âˆ‚(f, x)
         âˆ‚f*âˆ‚f'
       end;

julia> Î³(t) = [Ï€/4, t]; # circle of latitude

julia> U0 = [0., 1.]; # initial vector pointing east

julia> prob = ODEProblem(paralleltrans!, U0, 10, (g, Î³))
ODEProblem with uType Vector{Float64} and tType Int64. In-place: true
timespan: (0, 10)
u0: 2-element Vector{Float64}:
 0.0
 1.0

julia> sol = solve(prob);
```

The resulting solution object `sol` is matrix-like, as before. But we can also call it like `sol(t)` to get the interpolated approximation for a given time.

This means we can animate it, with a little extra setup code:

<figure>
  <video src="{{ site.github.url }}/projects/julia-general-relativity/sphere-transport.mp4" autoplay muted loop width="70%"/>
</figure>
